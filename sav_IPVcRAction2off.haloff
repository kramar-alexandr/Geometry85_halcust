external inner procedure WarnFutureDate(Boolean,Date);
external inner function Integer ARIInstallNr(LongInt,Date);
external inner updating function LongInt ARInstallVcRecordUpdateAfter(var record ARInstallVc,record ARInstallVc,LongInt,LongInt);
external inner function roundmode DefaultRoundMode();
external inner function Boolean FindHCUD(string,Date,string,var record HCUDVc);
external inner updating procedure UpdateBankTRFromIP(record IPVc);
external inner procedure IPGetPrepaymentBaseAmounts(LongInt,val,var val,var val);
external inner function string 255 CurDrawerCode(string);
external inner updating procedure IPUpdateCredMan(record IPVc,Boolean);
external inner updating procedure UpdateCUServiceIP(record IPVc,Boolean);
external inner updating procedure RecheckOpenCUServiceHistory(record CUServiceHistVc);
external inner updating procedure UpdateLiqInfoFromIP(LongInt,val,string,val,LongInt,LongInt,Boolean);
external inner procedure GetFullCurncyRate (var string,Date,var val,var val,var val,var val,var val);
external inner function string 255 GetCustomerOnAccountAC(string);
external inner procedure IPPastePayMode(var record IPVc);
external inner procedure GetARPayRate(LongInt,var val,var val,var val,var val,var val,string,Date);
external inner updating procedure ChangeCouponStateSL(var record IPVc);
external inner function boolean GenCouponsf();
external inner function val MulWithRateToBase1(var string,Date,val,roundmode);
external inner updating procedure SaveChequeLink2(record IPVc);
external inner function val MulRateToBase1(var string,val,val,val,val,val,val,roundmode);
external inner procedure SetIPFlags(record IPVc);
external inner updating procedure StoreUnOKHistory(string,LongInt,Date,Time,string);
external inner procedure IPSumup(var record IPVc);
//external inner updating procedure CustStatUp(string,string,Date,val,val,val,val,val,val,Boolean);
external inner updating procedure CustStatUp(string,LongInt,string,string,string,string,string,Date,val,val,val,val,val,val,Boolean);
external inner updating procedure DeleteTransaction(LongInt,Integer);
external inner procedure GetARAcc(string,var string);
external inner function Integer PMCheckType(string,var string,var string);
external inner procedure ShowUnOkWarn(string,Integer);

function LongInt IPRemovTest(record IPVc IPp,LongInt errmf)
BEGIN
  LongInt res;
  Integer i,rwcnt;
  record IVVc IVr;
  row IPVc IPrw;
  record DBLockBlock DBLockRec;
  
  res = 1;
  BlockLoad(DBLockRec);
  if ((IPp.ReceivedFlag!=0) or (IPp.OKFlag!=0)) then begin
    if (IPp.TransDate>DBLockRec.DeleteBeforeDate) then begin
      if (errmf>0) then begin
        MessageBox(1560,"");
      end;
      res = 0;
      goto LIPRemovTest;
    end;
    rwcnt = MatRowCnt(IPp);
    for (i=0;i<rwcnt;i=i+1) begin
      MatRowGet(IPp,i,IPrw);
      IVr.SerNr = IPrw.InvoiceNr;
      if (ReadFirstMain(IVr,1,true)) then begin
        if (errmf>0) then begin
          MessageBox(1560,"");
        end;
        res = 0;
        goto L66IPRemovTest;
      end;
    end;
L66IPRemovTest:;    
  end;
LIPRemovTest:;
  IPRemovTest = res;
  RETURN;
END;

global
function LongInt IPVcRecordRemoveTest(var record IPVc IPr,record IPVc IP2r,LongInt long3,LongInt long4)
BEGIN
  LongInt res;

  res = IPRemovTest(IPr,long3);
  IPVcRecordRemoveTest = res; 
  RETURN;
END;

global
updating function LongInt IPVcRecordRemoveAfter(var record IPVc IPr,record IPVc IP2r,LongInt stat,LongInt long4)
begin
  LongInt res;

  UpdateBankTRFromIP(IPr);  
  IPVcRecordRemoveAfter = res;
  return;
end;

global
procedure SetIPFlags(record IPVc IPp)
BEGIN
  switch (IPp.OKFlag) begin
    case 0: IPp.OKMark = 0;
    case 1: IPp.OKMark = 18;//checkMarkChar
  end;
  RETURN;
END;

global
updating procedure ARInstallmentAdd2(record ARVc ARp,Integer InstNr,val rval,val baserval,Boolean unokf)
BEGIN
  record ARInstallVc ARIr;
  record ARInstallVc oldARIr;
  Boolean found;
  val valincur;
  val valinbase;
  val t;
  Integer keys;
  string 255 ckey;
  
  ckey = "InvoiceNr";
  keys = 1;
  if (InstNr>0) then begin
    ckey = "InstallNr";
    keys = 2;
  end;
  if (unokf) then begin
    ARIr.InvoiceNr = ARp.InvoiceNr;
    ARIr.InstallNr = InstNr;
    found = ReadFirstKey(ckey,ARIr,keys,true);
    if (found) then begin
      RecordCopy(oldARIr,ARIr);
      ARIr.RVal = ARIr.RVal + rval;
      ARIr.BookRVal = ARIr.BookRVal + baserval;      
    end else begin
      ARIr.InvoiceNr = ARp.InvoiceNr;
      ARIr.DueDate = ARp.DueDate;
      found = ReadFirstKey("InvoiceNr",ARIr,2,true);
      if (found) then begin
        RecordCopy(oldARIr,ARIr);
        ARIr.RVal = ARIr.RVal + rval;
        ARIr.BookRVal = ARIr.BookRVal + baserval;      
      end else begin
        ARIr.InvoiceNr = ARp.InvoiceNr;
        ARIr.DueDate = ARp.DueDate;
        ARIr.CustCode = ARp.CustCode;
        ARIr.ARCurncyCode = ARp.ARCurncyCode;
        ARIr.RVal = rval;
        ARIr.BookRVal = baserval;
        ARIr.InstallNr = InstNr;
      end;
      if (ARIr.InstallNr<=0) then begin
        ARIr.InstallNr = ARIInstallNr(ARIr.InvoiceNr,ARIr.DueDate);
      end;
    end;
    if (found) then begin
      if (RecordUpdate(oldARIr,ARIr,false)==0) then begin
      end;
    end else begin
      if (RecordStore(ARIr,false)) then begin
      end;
    end;
  end else begin
    valincur = rval;
    valinbase = baserval;
    ARIr.InvoiceNr = ARp.InvoiceNr;
    ARIr.InstallNr = InstNr;
    found = true;
    while (LoopKey(ckey,ARIr,keys,found)) begin
      if (found) then begin
        if (ARIr.InvoiceNr!=ARp.InvoiceNr) then begin found = false; end;
        if (InstNr!=0) then begin
          if (ARIr.InstallNr!=InstNr) then begin found = false; end;
        end;
        if (valincur>=0) then begin found = false; end;
      end;
      if (found) then begin
        t = ARIr.RVal + valincur;
        if (t<=0) then begin
          RecordDelete(ARIr);
          StepBack(ARIr);
          valincur = valincur + ARIr.RVal;
          valinbase = valinbase + ARIr.BookRVal;
        end else begin
          RecordCopy(oldARIr,ARIr);
          ARIr.RVal = ARIr.RVal + valincur;
          ARIr.BookRVal = ARIr.BookRVal + valinbase;
          valincur = blankval;
          if (RecordUpdate(oldARIr,ARIr,false)==0) then begin end;
        end;
      end;
    end;
    if (valincur<0) then begin
      ARIr.InvoiceNr = ARp.InvoiceNr;
      if (ReadFIrstKey("InvoiceNr",ARIr,1,true)) then begin
        RecordCopy(oldARIr,ARIr);
        ARIr.RVal = ARIr.RVal + valincur;
        ARIr.BookRVal = ARIr.BookRVal + valinbase;
        ARInstallVcRecordUpdateAfter(ARIr,oldARIr,-1,-1);
      end;
    end;
  end;
  return;
end;

updating procedure ARAdd2(record IPVc IPp,row IPVc iprwp,val PV,Boolean importf,Boolean unokf)
BEGIN
  record ARVc ARr;
  record ARVc oldARr;
  record IVVc IVr;
  Boolean found;
  Boolean delf,ratef;
  val rs;
  val fr,br1,br2,to1,to2;
    
  if (PV!=0) then begin
    delf = false;
    ARr.InvoiceNr = iprwp.InvoiceNr;
    found = ReadFirstMain(ARr,1,true);
    RecordCopy(oldARr,ARr);
    if (found) then begin
      IVr.SerNr = iprwp.InvoiceNr;
      if (ReadFirstMain(IVr,1,true)) then begin
        if (IVr.Invalid!=0) then begin goto LARAdd2; end;
        fr = IVr.FrRate;
        to1 = IVr.ToRateB1;
        to2 = IVr.ToRateB2;
        br1 = IVr.BaseRate1;
        br2 = IVr.BaseRate2;
        ratef = true;
      end;
    end else begin
      IVr.SerNr = iprwp.InvoiceNr;
      if (ReadFirstMain(IVr,1,true)) then begin
        if (IVr.Invalid!=0) then begin goto LARAdd2; end;
        ARr.InvoiceNr = iprwp.InvoiceNr;
        ARr.CustCode = IVr.CustCode;
        ARr.CustName = IVr.Addr0;
        ARr.ARCurncyCode = IVr.CurncyCode;
        ARr.RVal = 0;
        ARr.BookRVal = 0;
        ARr.DueDate = IVr.PayDate;//CurrentDate;
		  // Заполнение поля проект в регистре неоплаченные сч-ф
		  ARr.PRCode = IVr.PRCode;
		  //
        if (IVr.pdrdays==-1) then begin
          ARr.ARRebDate = ARr.DueDate;
        end else begin
          ARr.ARRebDate = AddDay(IVr.InvDate,IVr.pdrdays);
        end;
        if (IVr.pdrdays2==-1) then begin
          ARr.ARRebDate2 = ARr.DueDate;
        end else begin
          ARr.ARRebDate2 = AddDay(IVr.InvDate,IVr.pdrdays2);
        end;
        fr = IVr.FrRate;
        to1 = IVr.ToRateB1;
        to2 = IVr.ToRateB2;
        br1 = IVr.BaseRate1;
        br2 = IVr.BaseRate2;
        ratef = true;
      end else begin
        goto LARAdd2;
      end;
    end;
    if (ratef) then begin
      rs = MulRateToBase1(iprwp.InvCurncy,PV,fr,to1,to2,br1,br2,DefaultCurRoundOff);
    end else begin
      rs = MulWithRateToBase1(iprwp.InvCurncy,IPp.TransDate,PV,DefaultCurRoundOff);
    end;
    ARr.RVal = ARr.RVal + PV;
    ARr.BookRVal = ARr.BookRVal + rs;
    if (importf==false) then begin
      ARInstallmentAdd2(ARr,iprwp.InstNr,PV,rs,unokf);
    end;
    if (ARr.RVal==0) then begin delf = true; end;
    if (ARr.InvoiceNr==-1) then begin goto LARAdd2; end;
    if (delf==false) then begin
      if (found) then begin
        if (RecordUpdate(oldARr,ARr,false)==0) then begin end;
      end else begin
        if (RecordStore(ARr,false)) then begin end;        
      end;
    end else begin
      if (found==true) then begin
        RecordDelete(oldARr);
      end;
    end;
      //add the code checking CUBalVc Date jj
  end;
LARAdd2:;
  RETURN;
END;

global
updating procedure UpdateORPrepaidAmount(LongInt ordnr,val rs)
BEGIN
  record ORVc ORr;
  record ORVc oldORr;
  
  if (ordnr>0) then begin
    ORr.SerNr = ordnr;
    if (ReadFirstMain(ORr,1,true)) then begin
      RecordCopy(oldORr,ORr);
      ORr.PrepaidAmount = ORr.PrepaidAmount + rs;
      if (RecordUpdate(oldORr,ORr,false)) then begin
      end;
    end;
  end;
  RETURN;
END;

function Date FindARPayHistDate(LongInt CUPNr)
begin
  Date res;
  record ARPayHistVc ARPayHistr;
  
  ARPayHistr.CUPNr = CUPNr;
  ARPayHistr.FileName = "IPVc";
  if (ReadFirstMain(ARPayHistr,2,true)) then begin
    res = ARPayHistr.TransDate;
  end;
  FindARPayHistDate = res;
  return;
end;

global
updating procedure ARPayAdd2(record IPVc IPp,row IPVc IPrwp,val PV,Boolean unokf)
BEGIN
  record BaseCurBlock bascur;
  record ARPayVc ARPayr;
  record ARPayVc oldARPayr;
  record CUVc CUr;
  Boolean found,delf;
  val rs,rs2;
  string 255 curcode;
  val from,to1,to2,base1,base2;

  BlockLoad(bascur);
  if (nonblank(bascur.StdBaseCur)) then begin
    curcode = bascur.StdBaseCur;
  end else begin
    curcode = bascur.BaseCur1;
  end;
  if (PV!=0) then begin
    delf = false;
    ARPayr.CUPNr = IPrwp.CUPNr;
    found = ReadFirstMain(ARPayr,1,true);
    CUr.Code = IPrwp.CustCode;
    RecordCopy(oldARPayr,ARPayr);
    if (found) then begin
    end else begin
      ARPayr.CUPNr = IPrwp.CUPNr;
      ARPayr.OrderNr = IPrwp.OrderNr;
      ARPayr.CustCode = IPrwp.CustCode;
      if (ReadFirstMain(CUr,1,true)) then begin
        ARPayr.CustName = CUr.Name;
      end;
      ARPayr.CurncyCode = IPrwp.InvCurncy;
      if (blank(IPrwp.InvCurncy)) then begin
        ARPayr.CurncyCode = IPrwp.RecCurncy;
      end;
      if (blank(ARPayr.CurncyCode)) then begin
        if (nonblank(curcode)) then begin
          ARPayr.CurncyCode = curcode;
        end;
      end;
      ARPayr.BankCurncy = IPrwp.BankCurncy;
      ARPayr.VATCode = IPrwp.VATCode;
      ARPayr.VATVal = 0;
      ARPayr.VATBookVal = 0;
      ARPayr.RVal = 0;
      ARPayr.BookRVal = 0;
      ARPayr.TAX1Sum = 0;
      ARPayr.TAX1SumBook = 0;
		//==== заполнение поля проект
		ARPayr.PRCode = IPrwp.PRCode;
		//====
    end;
    ARPayr.PayDate = FindARPayHistDate(IPrwp.CUPNr);
    if (blankdate(ARPayr.PayDate)) then begin
      ARPayr.PayDate = IPrwp.PayDate;
    end;
    if (blankdate(ARPayr.PayDate)) then begin ARPayr.PayDate = IPp.TransDate; end;
    ARPayr.RVal = ARPayr.RVal + PV;
    rs = MulWithRateToBase1(IPrwp.InvCurncy,ARPayr.PayDate,IPrwp.VATVal,DefaultCurRoundOff);

    ARPayr.VATVal = ARPayr.VATVal + IPrwp.VATVal;
    ARPayr.VATBookVal = ARPayr.VATBookVal + rs;

    ARPayr.TAX1Sum = ARPayr.TAX1Sum + IPrwp.TAX1Sum;
    rs = MulWithRateToBase1(IPrwp.InvCurncy,ARPayr.PayDate,IPrwp.TAX1Sum,DefaultCurRoundOff);
    ARPayr.TAX1SumBook = ARPayr.TAX1SumBook + rs;

    if (unokf) then begin
      IPGetPrepaymentBaseAmounts(IPrwp.CUPNr,IPrwp.InvVal,rs,rs2);
      if (PV<0) then begin
        rs = -rs;
      end;
    end else begin
      rs = MulWithRateToBase1(IPrwp.InvCurncy,ARPayr.PayDate,PV,DefaultCurRoundOff);
      if (nonblank(IPrwp.B1BankVal)) then begin
        rs = IPrwp.B1BankVal;
      end;
    end;
    rs = Round(rs,DefaultRoundMode);
    ARPayr.BookRVal = ARPayr.BookRVal + rs;
    if (ARPayr.RVal==0) then begin delf = true; end;
    if (delf==false) then begin
      if (found) then begin
        if (RecordUpdate(oldARPayr,ARPayr,false)==0) then begin end;
      end else begin
        if (RecordStore(ARPayr,false)) then begin end;        
      end;
    end else begin
      if (found==true) then begin
        RecordDelete(oldARPayr);
      end;
    end;
    if (IPrwp.OrderNr>0) then begin
      UpdateORPrepaidAmount(IPrwp.OrderNr,PV);
    end;
  end;
  RETURN;
END;

updating procedure ARPayHistAdd2(record IPVc IPp,Boolean unokf,Boolean importf)
BEGIN
  record BaseCurBlock bascur;
  record ARPayHistVc ARPayHistr;
  row IPVc IPrw;
  row IPVc IPrw2;
  Integer i,j,rwcnt;
  val rs,bookrs,vattot,b1bankv;
  string 255 curcode;
  val frrate,torate1,torate2,baserate1,baserate2;

  BlockLoad(bascur);
  if (nonblank(bascur.StdBaseCur)) then begin
    curcode = bascur.StdBaseCur;
  end else begin
    curcode = bascur.BaseCur1;
  end;
  rwcnt = MatRowCnt(IPp);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(IPp,i,IPrw);
    vattot = blankval;
    rs = blankval;
    bookrs = blankval;
    b1bankv = blankval;
    if (IPrw.CUPNr!=-1) then begin
//      rs = IPrw.RecVal;
//      bookrs = IPrw.BankVal;
//      vattot = IPrw.VATVal;

      for (j=i;j<rwcnt;j=j+1) begin
        MatRowGet(IPp,j,IPrw2);
        if (IPrw2.CUPNr==IPrw.CUPNr) then begin
          rs = rs + IPrw2.RecVal;//and what if diff rec curncy ? 
          bookrs = bookrs + IPrw2.BankVal;//and what if diff rec curncy ? 
          b1bankv = b1bankv + IPrw2.B1BankVal;
/*            
why should it be diffrent on import ? 
          if (IPrw2.B1BankVal!=0) then begin
            b1bankv = b1bankv + IPrw2.B1BankVal;
          end else begin
            if (importf==false) then begin   
              GetARPayRate(IPrw2.CUPNr,frrate,torate1,torate2,baserate1,baserate2,IPrw2.BankCurncy,IPp.TransDate);
              b1bankv = b1bankv + MulRateToBase1(IPrw2.BankCurncy,bookrs,frrate,torate1,torate2,baserate1,baserate2,DefaultCurRoundOff);
            end else begin
              b1bankv = b1bankv + MulWithRateToBase1(IPrw2.BankCurncy,IPp.TransDate,bookrs,DefaultCurRoundOff);
            end;
          end;          
*/          
          vattot = vattot + IPrw2.VATVal;//and what if diff rec curncy ? 
        end;
      end;

      ARPayHistr.CUPNr = IPrw.CUPNr;
      ARPayHistr.OrderNr = IPrw.OrderNr;
      ARPayHistr.FileName = "IPVc";
      ARPayHistr.SerNr = IPp.SerNr;
      ARPayHistr.TransDate = IPp.TransDate;
      ARPayHistr.CustCode = IPrw.CustCode;
      ARPayHistr.CurncyCode = IPrw.RecCurncy;
      ARPayHistr.BankCurncy = IPrw.BankCurncy;
      ARPayHistr.TAX1Sum = IPrw.TAX1Sum;
      ARPayHistr.Val = rs;
      if (bascur.BaseCur1==IPrw.BankCurncy) then begin
//        ARPayHistr.BookVal = bookrs;
        GetARPayRate(IPrw.CUPNr,frrate,torate1,torate2,baserate1,baserate2,IPrw.RecCurncy,ARPayHistr.TransDate);
        ARPayHistr.BookVal = MulRateToBase1(IPrw.RecCurncy,rs,frrate,torate1,torate2,baserate1,baserate2,DefaultCurRoundOff);        
        if (nonblank(b1bankv)) then begin 
          ARPayHistr.BookVal = b1bankv;
        end;
      end else begin
        if (importf==false) then begin
          GetARPayRate(IPrw.CUPNr,frrate,torate1,torate2,baserate1,baserate2,IPrw.BankCurncy,ARPayHistr.TransDate);
          if (IPrw.B1BankVal!=0) then begin
            ARPayHistr.BookVal = b1bankv;
          end else begin
//            ARPayHistr.BookVal = MulRateToBase1(IPrw.BankCurncy,bookrs,frrate,torate1,torate2,baserate1,baserate2,DefaultCurRoundOff);        
            ARPayHistr.BookVal = MulWithRateToBase1(IPrw.RecCurncy,ARPayHistr.TransDate,rs,DefaultCurRoundOff);
          end;
        end else begin
          if (IPrw.B1BankVal!=0) then begin
            ARPayHistr.BookVal = b1bankv;
          end else begin
//            ARPayHistr.BookVal = MulWithRateToBase1(IPrw.BankCurncy,ARPayHistr.TransDate,bookrs,DefaultCurRoundOff);
            ARPayHistr.BookVal = MulWithRateToBase1(IPrw.RecCurncy,ARPayHistr.TransDate,rs,DefaultCurRoundOff);
          end;
        end;
      end;
      ARPayHistr.VATVal = vattot;
      if (bascur.BaseCur1==IPrw.RecCurncy) then begin
        ARPayHistr.VATBookVal = vattot;
      end else begin
        ARPayHistr.VATBookVal = MulWithRateToBase1(IPrw.RecCurncy,ARPayHistr.TransDate,vattot,DefaultCurRoundOff);
      end;
      if (unokf) then begin      
        if (ReadFirstMain(ARPayHistr,0,true)) then begin
          RecordDelete(ARPayHistr);
        end;
      end else begin
        if (RecordStore(ARPayHistr,false)) then begin end;
      end;
    end;
  end;
  RETURN;
END;

updating procedure UpdateARFromIP(record IPVc IPp,Boolean unokf,Boolean importf)
BEGIN
  val v;
  row IPVc IPrw;
  Integer i,rwcnt;
  Boolean prepayexistf;

  prepayexistf = false;
  rwcnt = MatRowCnt(IPp);
  for (i=0; i<rwcnt;i=i+1) begin
    MatRowGet(IPp,i,IPrw);
    switch (IPrw.stp) begin
      case 5:
       if (IPrw.InvoiceNr!=-1) then begin
          v = -IPrw.InvVal;
          if (unokf) then begin
            v = IPrw.InvVal;
          end;
          ARAdd2(IPp,IPrw,v,importf,unokf);
        end;
      case 6:
       if (IPrw.InvoiceNr!=-1) then begin
          v = -IPrw.InvVal;
          if (unokf) then begin
            v = IPrw.InvVal;
          end;
          ARAdd2(IPp,IPrw,v,importf,unokf);
        end;
      case 1:
        v = IPrw.InvVal;
        if (unokf) then begin
          v = -IPrw.InvVal;
        end;
        if (IPrw.InvoiceNr!=-1) then begin
          v = -v;
          ARAdd2(IPp,IPrw,v,importf,unokf);
        end else begin
          if ((IPrw.CUPNr!=-1) and (IPrw.InvoiceNr==-1)) then begin
            ARPayAdd2(IPp,IPrw,v,unokf);
            prepayexistf = true;
          end;
        end;
    end;
  end;
  if (prepayexistf) then begin
    ARPayHistAdd2(IPp,unokf,importf);
  end;
  RETURN;
END;

updating procedure UpdateIPChecks(record IPVc IPp,Boolean unokf)
BEGIN
  record CheckVc Checkr;
  record CouponVc Couponr;
  record IVVc IVr;
  Integer i,rwcnt;
  row IPVc IPrw;
  Integer cht;
  string 10 accnr;
  string 60 objs;
  Date bd;

  rwcnt = MatRowCnt(IPp);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(IPp,i,IPrw);
    if (nonblank(IPrw.PayMode)) then begin
      cht = PMCheckType(IPrw.PayMode,accnr,objs);
    end else begin
      cht = PMCheckType(IPp.PayMode,accnr,objs);
    end;
    switch (cht) begin
      case 1:
        if (IPrw.stp==1) then begin
          Checkr.SerNr = IPrw.ChequeNr;
          if (ReadFirstMain(Checkr,1,true)) then begin
            Checkr.OrigAccNr = "";
            IVr.SerNr = IPrw.InvoiceNr;
            if (ReadFirstMain(IVr,1,true)) then begin
              Checkr.OrigAccNr = IVr.ARAcc;
            end;
            if (blank(Checkr.TempAccNr)) then begin Checkr.TempAccNr = accnr; end;
            if (blank(Checkr.TempObjects)) then begin Checkr.TempObjects = objs; end;
            if (blank(Checkr.CUCode)) then begin
              Checkr.CUCode = IPrw.CustCode;
              if (blank(Checkr.CUName)) then begin Checkr.CUName = IPrw.CustName; end;
            end;        
            if (IPrw.RecVal<0) then begin
              Checkr.Openf = 5;
            end else begin
              Checkr.Openf = 1;
            end;
            if (unokf) then begin
              Checkr.OrigAccNr = "";
              Checkr.TempAccNr = "";
              Checkr.TempObjects = "";
//              Checkr.CUCode = "";
//              Checkr.CUName = "";
              Checkr.Openf = 0;
            end;
            if (RecordStore(Checkr,true)) then begin end;
          end;
        end;
      case 3:
        if (IPrw.stp==1) then begin
          Couponr.SerNr = IPrw.ChequeNr;
          if (ReadFirstMain(Couponr,1,true)) then begin
            if (unokf) then begin
              Couponr.Status = 0;
              Couponr.DepDate = bd;
            end else begin
              Couponr.Status = 1;
            end;
            if (RecordStore(Couponr,true)) then begin end;
          end;
        end;
    end;
L88UpdateIPChecks:;
  end;
  RETURN;
END;

global
updating procedure IPOK(record IPVc IPp,record IPVc IP2p,Boolean updatef)
BEGIN
  if (IPp.OKFlag==1) then begin
    UpdateARFromIP(IPp,false,false);
    UpdateCUServiceIP(IPp,false);
  end;
  UpdateIPChecks(IPp,false);
  RETURN;
END;

updating procedure IPUnOK(record IPVc IPp)
BEGIN
  UpdateARFromIP(IPp,true,false);
  UpdateCUServiceIP(IPp,true);
  UpdateIPChecks(IPp,true);
  RETURN;
END;

global
updating function LongInt IPVcRecordSave(var record IPVc IPr,record IPVc IP2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;

  SetIPFlags(IPr);
  if (blanktime(IPr.RegTime)) then begin
    IPr.RegTime = CurrentTime;
  end;
  if (IPr.OKFlag!=0) then begin
//      ((IPr.ReceivedFlag==1) and (SupportTempBook(IPp,IPVc)))) then begin /* not needed I think */
    IPOK(IPr,IP2r,false);
  end;
  IPVcRecordSave = res;
  RETURN;
END;

global
updating procedure IPrsStats(record IPVc IPp,Boolean deletef,Boolean updateliqinfof)
BEGIN
  record IPrsVc mainr;
  record IPrsVc IPrsr;
  record IVVc IVr;
  row IPVc IPrw,IP2rw;
  Integer i,rwcnt,j;
  Boolean found;
  string 255 tstr;
  val invval;
  
  rwcnt = MatRowCnt(IPp);
  for (i=0; i<rwcnt;i=i+1) begin
    MatRowGet(IPp,i,IPrw);
    switch (IPrw.stp) begin
      case 5:
          for (j = 0;j<rwcnt;j=j+1) begin
            if (j!=i) then begin
              MatRowGet(IPp,j,IP2rw);
              if (IP2rw.stp==1) then begin
                if (IP2rw.InvoiceNr==IPrw.InvoiceNr) then begin
                  found = true;
                end;
              end;
            end;
          end;
          if (found==true) then begin
          end else begin
            goto LIPrsStatscase1;
          end;
          // else we will continue and execute the code for case 1 
      case 6: 
          for (j = 0;j<rwcnt;j=j+1) begin
            if (j!=i) then begin
              MatRowGet(IPp,j,IP2rw);
              if (IP2rw.stp==1) then begin
                if (IP2rw.InvoiceNr==IPrw.InvoiceNr) then begin
                  found = true;
                end;
              end;
            end;
          end;
          if (found==true) then begin
          end else begin
            goto LIPrsStatscase1;
          end;
          // else we will continue and execute the code for case 1 
      case 7: 
          for (j = 0;j<rwcnt;j=j+1) begin
            if (j!=i) then begin
              MatRowGet(IPp,j,IP2rw);
              if (IP2rw.stp==1) then begin
                if (IP2rw.InvoiceNr==IPrw.InvoiceNr) then begin
                  found = true;
                end;
              end;
            end;
          end;
          if (found==true) then begin
          end else begin
            goto LIPrsStatscase1;
          end;
          // else we will continue and execute the code for case 1 
      case 1:
LIPrsStatscase1:;      
        if (IPrw.ovst==0) then begin
          if (IPrw.InvoiceNr==-1) then begin
            GetARAcc(IPrw.CustCode,tstr);
            mainr.AccNr = tstr;
          end else begin
            IVr.SerNr = IPrw.InvoiceNr;
            if (ReadFirstMain(IVr,1,true)) then begin
              mainr.AccNr = IVr.ARAcc;
            end else begin    
              IPrsr.IVNr = IPrw.InvoiceNr;
              if (ReadFirstKey("IVKey",IPrsr,1,true)==false) then begin
                goto L88IPrsStats;
              end;  
            end;  
          end;
          mainr.CustCode = IPrw.CustCode;
          if (blankdate(IPrw.PayDate)) then begin
            mainr.CustDate = IPp.TransDate;
          end else begin
            mainr.CustDate = IPrw.PayDate;
          end;
          mainr.TransType = 1;
          mainr.TransNr = IPp.SerNr;
          mainr.IVNr = IPrw.InvoiceNr;
          mainr.TransDate = IPp.TransDate;
          if (nonblank(IPrw.PayMode)) then begin
            mainr.CheckType = PMCheckType(IPrw.PayMode,tstr,tstr);
          end else begin
            mainr.CheckType = PMCheckType(IPp.PayMode,tstr,tstr);
          end;
          mainr.ChequeNr = IPrw.ChequeNr;

//          mainr.AccNr = IVr.ARAcc; it is above
          if (deletef) then begin
            if (ReadFirstMain(mainr,0,true)) then begin
              RecordDelete(mainr);
            end;
          end else begin
            if (RecordStore(mainr,false)) then begin end;
          end;
          if (updateliqinfof) then begin
            invval = IPrw.InvVal;
            for (j=i+1;j<rwcnt;j=j+1) begin
              MatRowGet(IPp,j,IP2rw);  
              if (IP2rw.InvoiceNr==IPrw.InvoiceNr) then begin
                switch (IP2rw.stp) begin
                  case 5: invval = invval + IP2rw.InvVal;
                  case 6: invval = invval + IP2rw.InvVal;
                  otherwise j = rwcnt;
                end;
              end;
            end;
            UpdateLiqInfoFromIP(IPrw.InvoiceNr,invval,IPrw.InvCurncy,blankval,IPp.SerNr,i,deletef);
          end;
        end;
    end;
L88IPrsStats:;
  end;
  RETURN;
END;

updating procedure UpdateJobFromIP(record IVVc IVr,row IPVc IPrw)
begin
  record JobVc Jobr;
  record JobVc oldJobr;
  record HotelBlock Hotelb;
  record HCUDVc HCUDr;
  string 255 rsccode,resstatus;
  record RSCVc RSCr;
  record CUVc CUr;
  row RSCVc RSCrw;
  Integer i,rwcnt;
  record ARVc ARr;
  val prc,lastrscprc;
  Boolean updatef,jobf;
  
  if (IVr.InvType==kInvoiceTypeDownpayment) then begin
    BlockLoad(Hotelb);
    Jobr.SerNr = IVr.JobNr;
    jobf = ReadFirstMain(Jobr,1,true);
    FindHCUD(IVr.CustCode,IVr.InvDate,Jobr.ResStatus,HCUDr);
    rsccode = HCUDr.RSCCode;
    if (blank(rsccode)) then begin      
      if (jobf) then begin
        CUr.Code = Jobr.CUCode;
        if (ReadFirstMain(CUr,1,true)) then begin
          if (CUr.CustCat==Hotelb.WebDefCustCat) then begin
            rsccode = Hotelb.WebRSCCode;
          end;
        end;
      end;
    end;
    if (blank(rsccode)) then begin
      rsccode = Hotelb.RSCCode;
    end;
    if (nonblank(rsccode)) then begin
      RSCr.Code = rsccode;
      if (ReadFirstMain(RSCr,1,true)) then begin
        ARr.InvoiceNr = IVr.SerNr;
        ReadFirstMain(ARr,1,true);
        prc = ((IVr.Sum4-ARr.RVal)*100)/IVr.Sum4;
        rwcnt = MatRowCnt(RSCr);
        for (i=0;i<rwcnt;i=i+1) begin
          MatRowGet(RSCr,i,RSCrw);
          if (RSCrw.Percentage>lastrscprc) then begin
            if (prc>=RSCrw.Percentage) then begin
              updatef = true;
              resstatus = RSCrw.ResStatus;
            end;
          end;
          lastrscprc = RSCrw.Percentage;
        end;
        if (updatef) then begin
          if (nonblank(resstatus)) then begin
            Jobr.SerNr = IVr.JobNr;
            if (ReadFirstMain(Jobr,1,true)) then begin
              RecordCopy(oldJobr,Jobr);
              Jobr.ResStatus = resstatus;
              if (RecordUpdate(oldJobr,Jobr,true)) then begin
              end;
            end;
          end;
        end;
      end;
    end;
  end;
  return;
end;

global
updating procedure UpdateCSFromIP(record IPVc IPp,Boolean unokf)
BEGIN
  row IPVc IPrw;
  record IVVc IVr;
  val t,v,bv;
  val sum;
  Integer i,rwcnt;
  Boolean testf,negf;
  record BaseCurBlock bcur;
  record CUVc CUr;
  val frrate,torate1,torate2,baserate1,baserate2;

  negf = true;
  BlockLoad(bcur);
  if (unokf) then begin
    negf = false;
  end;
  rwcnt = MatRowCnt(IPp);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(IPp,i,IPrw);
    testf = false;
    switch (IPrw.stp) begin
      case 1: testf = true;
      case 5: testf = true;
      case 6: testf = true;
//      case 7:// idon't think fee is needed here
    end;
    if (testf) then begin
        t = 0;
        sum = 0;
        if (IPrw.InvoiceNr!=-1) then begin
          v = -IPrw.InvVal;
          IVr.SerNr = IPrw.InvoiceNr;
          if (ReadFirstMain(IVr,1,true)) then begin
            bv = IPrw.InvVal;
            CUr.Code = IVr.CustCode;
            if (ReadFirstMain(CUr,1,true)) then begin end;
            if ((blank(CUr.CurncyCode)) or (CUr.CurncyCode==bcur.BaseCur1)) then begin          
              v = MulRateToBase1(IVr.CurncyCode,v,IVr.FrRate,IVr.ToRateB1,IVr.ToRateB2,IVr.BaseRate1,IVr.BaseRate2,DefaultCurRoundOff);
            end;
            bv = MulRateToBase1(IVr.CurncyCode,bv,IVr.FrRate,IVr.ToRateB1,IVr.ToRateB2,IVr.BaseRate1,IVr.BaseRate2,DefaultCurRoundOff);
/*            
            if ((bcur.BaseCur1==IPrw.BankCurncy) or (blank(IPrw.BankCurncy))) then begin
              bv = IPrw.BankVal;//must be with invoice rate
            end;          
*/            
//negf makes everything negative
            CustStatUp(IPrw.CustCode,IPrw.CustCode,IPp.TransDate,t,t,v,t,bv,sum,negf);
            if (IVr.JobNr>0) then begin
              UpdateJobFromIP(IVr,IPrw);
            end;
          end;
        end else begin
          v = -IPrw.InvVal;
          GetARPayRate(IPrw.CUPNr,frrate,torate1,torate2,baserate1,baserate2,IPrw.InvCurncy,IPp.TransDate);
          bv = MulRateToBase1(IPrw.InvCurncy,IPrw.InvVal,frrate,torate1,torate2,baserate1,baserate2,DefaultCurRoundOff);
          CUr.Code = IPrw.CustCode;
          if (ReadFirstMain(CUr,1,true)) then begin end;
          if (nonblank(CUr.CurncyCode)) and (CUr.CurncyCode!=bcur.BaseCur1) then begin
          end else begin          
            if ((blank(CUr.CurncyCode)) or (CUr.CurncyCode==bcur.BaseCur1)) then begin          
              v = MulRateToBase1(IPrw.InvCurncy,v,frrate,torate1,torate2,baserate1,baserate2,DefaultCurRoundOff);
            end;
            if ((bcur.BaseCur1==IPrw.BankCurncy) or (blank(IPrw.BankCurncy))) then begin
              v = -IPrw.BankVal;
            end;
          end;
          CustStatUp(IPrw.CustCode,IPrw.CustCode,IPp.TransDate,t,t,v,t,bv,sum,negf);
        end;
    end;
  end;
  RETURN;
END;

global
updating function LongInt IPVcRecordSaveAfter(var record IPVc IPr,record IPVc IP2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;

  if (IPr.OKFlag!=0) then begin
    IPrsStats(IPr,false,true);
    UpdateCSFromIP(IPr,false);
    SaveChequeLink2(IPr);    
    IPUpdateCredMan(IPr,true);
  end;
  
  if (GenCouponsf) then begin
    ChangeCouponStateSL(IPr);
  end;
        
  IPVcRecordSaveAfter = res;
  RETURN;
END;

global
updating function LongInt IPVcRecordImportAfter(var record IPVc IPr,record IPVc IP2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;

  if (IPr.OKFlag!=0) then begin
    IPrsStats(IPr,false,false);
    UpdateCSFromIP(IPr,false);
  end;
  IPVcRecordImportAfter = res;
  RETURN;
END;

global
updating function LongInt IPVcRecordUpdate(var record IPVc IPr,record IPVc IP2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;

  SetIPFlags(IPr);
  if ((IPr.OKFlag!=0) and (IP2r.OKFlag==0)) then begin
    IPOK(IPr,IP2r,true);
  end;
  if ((IPr.OKFlag==0) and (IP2r.OKFlag!=0)) then begin//unok
    DeleteTransaction(IPr.SerNr,IPYc);
    IPUnOK(IPr);
  end;
  IPVcRecordUpdate = res;
  RETURN;
END;

global
updating function LongInt IPVcRecordUpdateAfter(var record IPVc IPr,record IPVc IP2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;

  if ((IPr.OKFlag!=0) and (IP2r.OKFlag==0)) then begin
    IPrsStats(IPr,false,true);
    UpdateCSFromIP(IPr,false);
    SaveChequeLink2(IPr);    
    IPUpdateCredMan(IPr,true);
    if (GenCouponsf) then begin
      ChangeCouponStateSL(IPr);
    end;    
  end;
  if ((IPr.OKFlag==0) and (IP2r.OKFlag==1)) then begin//unok
    IPrsStats(IPr,true,true);    
    UpdateCSFromIP(IPr,true);
//    SaveChequeLink2(IPr);    //?
    IPUpdateCredMan(IPr,false);
    StoreUnOKHistory("IPVc",IPr.SerNr,CurrentDate,CurrentTime,CurrentUser);
  end;
  IPVcRecordUpdateAfter = res;
  RETURN;
END;

global
function LongInt IPVcRecordReset(var record IPVc IPr,record IPVc IP2r,LongInt long3,LongInt long4)
begin
  LongInt res;
  
  IPr.OKFlag = 0;
  IPVcRecordReset = res;
  RETURN;
END;

procedure ConvertIPtoDualBase(record IPVc IPp)
BEGIN
  record BaseCurBlock bascur;
  row IPVc IPrw;
  Integer rwcnt,i;

  BlockLoad(bascur);
  rwcnt = MatRowCnt(IPp);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(IPp,i,IPrw);
    if (blank(IPrw.InvCurncy)) then begin
      IPrw.InvCurncy = bascur.BaseCur1;
    end;
    if (blank(IPrw.RecCurncy)) then begin
      IPrw.RecCurncy = bascur.BaseCur1;
    end;
    if (blank(IPrw.BankCurncy)) then begin
      IPrw.BankCurncy = bascur.BaseCur1;
    end;
    MatRowPut(IPp,i,IPrw);
  end;
  IPSumup(IPp);
  RETURN;
END;

procedure ReadIPFunctions(var record IPVc IPp)
BEGIN
  IPSumup(IPp);
  SetIPFlags(IPp);
  RETURN;
END;

global
updating function LongInt IPVcRecordImport(var record IPVc IPr,record IPVc IP2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  Boolean gToDualBase;
  record ConvMasterBlock cvm;
  Integer i,rwcnt;
  row IPVc IPrw;
      
//  ReadIPFunctions(IPr);
  BlockLoad(cvm);
  if (cvm.DualBaseCurrencyFlag!=0) then begin gToDualBase = true; end;
  if (gToDualBase) then begin
    ConvertIPtoDualBase(IPr);
  end;
  rwcnt = MatRowCnt(IPr);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(IPr,i,IPrw);
    switch (IPrw.stp) begin
      case 1:
        if (blankdate(IPrw.PayDate)) then begin
          IPrw.PayDate = IPr.TransDate;
        end;
        if (IPrw.ChequeNr==0) then begin IPrw.ChequeNr = -1; end;
        if (IPrw.CUPNr==0) then begin IPrw.CUPNr = -1; end;
        if (IPrw.InstNr<=0) then begin IPrw.InstNr = IPrw.InvoiceNr; end;
      case 5:
        IPrw.latedays = -1;
        IPrw.PayDate = IPr.TransDate;
      case 6:
        IPrw.latedays = -1;
        IPrw.PayDate = IPr.TransDate;
      case 7:
        IPrw.latedays = -1;
        IPrw.PayDate = IPr.TransDate;
    end;
/*
    if (blank(IPrw.PayMode)) then begin
      IPrw.PayMode = IPr.PayMode;
    end;
*/
    MatRowPut(IPr,i,IPrw);
  end;
  IPSumup(IPr);
  SetIPFlags(IPr);
  if (((IPr.OKFlag==1) or (IPr.ReceivedFlag==1)) and (IPr.RejectedFlag==0)) then begin
    UpdateARFromIP(IPr,false,true);
  end;
  if (IPr.OKFlag!=0) then begin
    SaveChequeLink2(IPr);
  end;  
  IPVcRecordImport = res;
  RETURN;
END;

global
function LongInt IPVcRecordDefaults(var record IPVc IPr,record IPVc IP2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  record AccBlock ARAccRec;
  
  IPr.SerNr = -1;
  BlockLoad(ARAccRec);
  IPr.ARonTR = ARAccRec.ARonTR;
  IPr.RegDate = CurrentDate;
  IPr.TransDate = CurrentDate;
  if (SingleUserMode) then begin
    IPr.SerNr = NextSerNr("IPVc",IPr.TransDate,-1,false,"");
  end;
  IPr.PayMode = "";
  IPr.Comment = "";
  IPPastePayMode(IPr);
  IPr.OKFlag = 0;
  IPr.MachineName = CurMachineName;
  IPr.DrawerCode = CurDrawerCode(IPr.MachineName);
  IPVcRecordDefaults = res; 
  RETURN;
END;

global
function LongInt IPVcRecordDuplicate(var record IPVc IPr,record IPVc IP2r,LongInt stat,LongInt long4)
BEGIN
  LongInt res;
  
  IPr.SerNr = -1;
  IPr.RegDate = CurrentDate;
  IPr.TransDate = CurrentDate;
  if (SingleUserMode) then begin
    IPr.SerNr = NextSerNr("IPVc",IPr.TransDate,-1,false,"");
  end;
  IPr.OKFlag = 0;
  IPr.ExportedFlag = 0;
  IPr.ReceivedFlag = 0;
  IPr.RejectedFlag = 0;
  IPr.AuthorizationCode = "";
  IPPastePayMode(IPr);
  IPr.MachineName = CurMachineName;
  IPr.DrawerCode = CurDrawerCode(IPr.MachineName);
  WarnFutureDate(true,IPr.TransDate);
  IPVcRecordDuplicate = res; 
  RETURN;
END;

global
procedure IVVATPart(LongInt invnr,val rsp,var val vsump)
BEGIN
  record IPrsVc IPrsr;
  record IVVc IVr;
  Boolean found;
  val ivsum,vatsum,t;
  
  ivsum = blankval;
  vatsum = blankval;
  IPrsr.IVNr = invnr;
  found = true;
  while (LoopKey("IVKey",IPrsr,1,found)) begin
    if (IPrsr.IVNr!=invnr) then begin
      found = false;
    end;
    if (found) then begin
      switch (IPrsr.TransType) begin
        case 0:
          IVr.SerNr = IPrsr.TransNr;
          if (ReadFirstMain(IVr,1,true)) then begin
            if (IVr.Invalid==0) then begin
              if (IVr.InvType==3) then begin
                ivsum = ivsum - IVr.Sum4;
                vatsum = vatsum - IVr.Sum3;
              end else begin
                ivsum = ivsum + IVr.Sum4;
                vatsum = vatsum + IVr.Sum3;
                t = MulRateToBase1(IVr.CurncyCode,rsp,IVr.FrRate,IVr.ToRateB1,IVr.ToRateB2,IVr.BaseRate1,IVr.BaseRate2,DefaultCurRoundOff);
              end;
            end;
          end;
      end;
    end;
  end;
  vsump = t*vatsum;
  vsump = vsump/ivsum;
  vsump = Round(vsump,DefaultRoundMode);  
  RETURN;
END;

global
function LongInt IPVcRecordSaveClient(LongInt wn,var record IPVc IPr,record IPVc IP2r,LongInt long3,LongInt long4)
begin
  LongInt res;
  
  if (IPr.OKFlag!=0) then begin
    UpdateBrowses("IVVc");
  end;
  ShowUnOkWarn("IPVc",IPr.OKFlag);
  
  IPVcRecordSaveClient = res;
  return;
end;

global
updating function LongInt IPVcRecordUpdateClient(LongInt wn,var record IPVc IPr,record IPVc IP2r,LongInt stat,LongInt long4)
begin
  LongInt res;
  
  if ((IPr.OKFlag!=0) and (IP2r.OKFlag==0)) then begin
    UpdateBrowses("IVVc");
  end;
  ShowUnOkWarn("IPVc",IPr.OKFlag);
  
  IPVcRecordUpdateClient = res;
  return;
end;
